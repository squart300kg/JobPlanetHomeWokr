plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
}

// (서버 확장 대비)flavorType에 따라 다음의 방식으로 URL이 설정됩니다.
// ex). dev ->https://dev-jpassets.jobplanet.co.kr/mobile-config/
// ex). stage ->https://stage-jpassets.jobplanet.co.kr/mobile-config/
// ex). open ->https://jpassets.jobplanet.co.kr/mobile-config/
def getSourceBaseUrl(flavorType = "") {
    return "https://"+flavorType+"jpassets.jobplanet.co.kr/mobile-config/"
}

def getDate() {
    return new Date().format("MMdd")
}

android {
    compileSdkVersion 32
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.jobplanet.kr.android"
        minSdkVersion 24
        targetSdkVersion 32
        versionCode 1
        versionName "1.0"
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    /**
     * 서버 확장 및 앱 버전 관리 대비
     */
    flavorDimensions "basic"
    productFlavors {
        dev {
            dimension "basic"
            versionNameSuffix "(${getDate()})-dev"
            applicationIdSuffix ".dev"
            manifestPlaceholders = [ appName : "잡플래닛(dev)" ]

            buildConfigField "String", "SERVICE_BASE_URL", "\"${getSourceBaseUrl()}\""

        }
        qa {
            dimension "basic"
            versionNameSuffix "(${getDate()})-qa"
            applicationIdSuffix ".qa"
            manifestPlaceholders = [ appName : "잡플래닛(qa)" ]

            buildConfigField "String", "SERVICE_BASE_URL", "\"${getSourceBaseUrl()}\""

        }
        open {
            dimension "basic"
            manifestPlaceholders = [ appName : "잡플래닛" ]

            buildConfigField "String", "SERVICE_BASE_URL", "\"${getSourceBaseUrl()}\""

        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        dataBinding true
    }

}
kapt {
    correctErrorTypes true
}
dependencies {

    // LOCAL
    implementation 'com.scottyab:secure-preferences-lib:0.1.7'

    // NETWORK
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.7.2'
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'androidx.paging:paging-runtime-ktx:3.1.1'

    // GLIDE
    implementation 'com.github.bumptech.glide:glide:4.14.2'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.14.2'

    // DI
    implementation "com.google.dagger:hilt-android:2.44"
    kapt "com.google.dagger:hilt-compiler:2.44"

    // UI
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.3'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'

    // TEST
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.5.0'

}